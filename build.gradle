buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "java"
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: "maven-publish"
apply plugin: "signing"

group 'com.github.isyscore'
version '2.2.4.3'

repositories {
    mavenCentral()
}

dependencies {
    api 'com.squareup.okhttp3:okhttp:4.12.0'
    api "com.squareup.okio:okio-jvm:3.7.0"
    api 'commons-io:commons-io:2.16.0'
    api 'org.apache.commons:commons-compress:1.26.1'
    api 'org.apache.commons:commons-lang3:3.13.0'
    api 'org.jsoup:jsoup:1.16.1'
    api 'com.google.code.gson:gson:2.10.1'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    api "org.ktorm:ktorm-core:3.6.0"
    api "org.ktorm:ktorm-jackson:3.6.0"
    api "com.alibaba:druid:1.2.18"
    api "com.fasterxml.jackson.core:jackson-databind:2.15.3"
    api "com.squareup.okhttp3:okhttp-sse:4.12.0"
    testImplementation 'junit:junit:4.13.2'
}

sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions.jvmTarget = "1.8" // "17"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8" // "17"
}

task sourcesJar(type: Jar) {
    archiveClassifier = "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenKotlin(MavenPublication) {
            groupId = "com.github.isyscore"
            artifactId = "common-jvm"
            version = "2.2.4.3"
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "common-jvm"
                description = "iSysCore Common Kotlin Library"
                url = "https://github.com/isyscore/common-jvm"
                packaging = "jar"

                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/isyscore/common-jvm/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "isyscore"
                        name = "isyscore"
                        email = "hexj@isyscore.com"
                    }
                }
                scm {
                    connection = "https://github.com/isyscore/common-jvm"
                    developerConnection =  "https://github.com/isyscore/common-jvm"
                    url = "https://github.com/isyscore/common-jvm"
                }
            }
        }
    }
    repositories {
        maven {
            name = "Release"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = "$nexusUsername"
                password = "$nexusPassword"
            }
        }
        maven {
            name = "Snapshot"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username = "$nexusUsername"
                password = "$nexusPassword"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenKotlin
}
